// プログラム全体
program         = { function_def | stmt } ;

// 関数シグネチャ（定義と宣言で共通）
function_sig     = type declarator;

// 関数定義
function_def     = function_sig "{" { stmt } "}" ;

// 関数のプロトタイプ宣言
prototype_decl   = function_sig ";" ;


// (文)statement
stmt            = expr ";"                        // 式文
                | decl_stmt                       // 宣言文
                | "return" expr ";"               // return 文
                | "if" "(" expr ")" stmt ["else" stmt]    // if 文
                | "while" "(" expr ")" stmt       // while 文
                | "do" stmt "while" "(" expr ")" ";" // do-while 文
                | "for" "(" [expr] ";" [expr] ";" [expr] ")" stmt // for 文
                | "continue" ";"                  // continue
                | "break" ";"                     // break
                | "goto" ident ";"                // goto 文
                | ident ":" stmt                  // ラベル
                | "{" { stmt } "}"                // ブロック文
                | "switch" "(" expr ")" "{" { case_clause } "}" ; // ← switch 文追加

case_clause     = "case" expr ":" { stmt }                  // case 節
                | "default" ":" { stmt } ;                  // default 節

// 宣言文
decl_stmt         = typedef_stmt
                  |struct_def
                  | union_def
                  | enum_def
                  | type init_declarator { "," init_declarator } ";" ;
init_declarator   = declarator [ "=" initializer  ] ;
initializer       = expr | "{" initializer_list "}" ;
initializer_list  = initializer { "," initializer } ;
declarator        = { "*" } [direct_declarator] ;
direct_declarator = ident
                  | "(" declarator ")"                      // 関数ポインタなど
                  | direct_declarator "[" [expr] "]"        // 配列
                  | direct_declarator "(" [param_list] ")" ;// 関数

// typedef 宣言
typedef_stmt      = "typedef" typedef_type declarator { "," declarator } ";" ;
typedef_type      = type | struct_def | union_def | enum_def ;

// 構造体 / 共用体定義
struct_def        = "struct" [ident] "{" { decl_member  } "}" ";" ;
union_def         = "union"  [ident] "{" { decl_member  } "}" ";" ;

decl_member       = type declarator { "," declarator } ";" ;

// enum 定義
enum_def          = "enum" [ident] "{" { enum_member } "}" ";" ;
enum_member       = ident [ "=" num ] { "," ident [ "=" num ] } ;

// 型
type = "int" | "double" | "char" | "void" | "struct" ident | "union" ident | "enum" ident ;

// 式(expression)
expr            = comma ;
comma           = assign { "," assign } ;
assign          = conditional
                [ assign_op assign ] ;
assign_op       = "="
                | "+=" | "-=" | "*=" | "/=" | "%="
                | "^=" | "|=" | "&="
                | "<<=" | ">>=" ;
conditional     = logical_or [ "?" expr ":" conditional ] ;
logical_or      = logical_and { "||" logical_and } ;
logical_and     = equality    { "&&" equality } ;
equality        = relational { ("==" | "!=") relational } ;
relational      = bitwise_or { ("<" | "<=" | ">" | ">=") bitwise_or } ;
bitwise_or      = bitwise_xor { "|" bitwise_xor } ;
bitwise_xor     = bitwise_and { "^" bitwise_and } ;
bitwise_and     = bitwise_shift { "&" bitwise_shift } ;
bitwise_shift   = add { ("<<" | ">>") add } ;

add             = mul { ("+" | "-") mul } ;
mul             = unary { ("*" | "/" | "%") unary } ;
unary           = ( "++" | "--" | "+" | "-" | "*" | "&" | "!" | "~" ) unary
                | "sizeof" expr           
                | "sizeof" "(" type ")"
                |"(" type ")" unary    
                | postfix ;



//後置式(postfix)
postfix = primary { postfix_suffix } ;

postfix_suffix =
        "[" expr "]"
      | "(" [ arg_list ] ")"
      | "++"
      | "--"
      | "." ident
      | "->" ident ;

// プライマリ式(関数呼び出し・識別子・数値など)
primary         = ident                       // 変数
                | int                         // 整数リテラル
                | float                    // 文字
                | "(" expr ")"                // 括弧つき式
                | string_lit;

// 関数の引数・引数リスト
arg_list        = expr { "," expr } ;
param_list      = param { "," param } ;
param           = type [declarator];


// 終端記号
int         = /[0-9]+/ ;
float       = /[0-9]*\.[0-9]+([eE][+-]?[0-9]+)?/ ;
ident       = /[a-zA-Z_][a-zA-Z0-9_]*/ ;
char_lit    = /'(\\.|[^\\'])'/ ;
string_lit  = /"([^"\\]|\\.)*"/ ;